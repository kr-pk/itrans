boolean dodraw;
dodraw=true;
boolean goleft;
goleft=false;
def tthinnest=max(hair,2) enddef;
def tthinner=max(1.4hair,2) enddef;
def tthin=max(1.8hair,2) enddef;
def tmed=max(.75curve,2) enddef;
def  tthick=curve enddef;
def tthicker=1.2curve enddef;
def tthickest=1.4curve enddef;

string callchar;
numeric oldh; numeric oldw; numeric oldd;

def charspec(expr c, w_sharp, h_sharp, d_sharp) =
  oldh:=h; oldw:=w; oldd:=d;
  w:=hround(w_sharp*hppp); h:=vround(h_sharp*hppp); d:=vround(d_sharp*hppp);  
enddef;

input tamilmax;

def cloney(suffix $,@) =
  y$=y@; y$r=y@r; y$l=y@l;
enddef;

def clonex(suffix $,@) =
  x$=x@; x$r=x@r; x$l=x@l;
enddef;

def small_iito (suffix @,$)(expr iheight) =
x$99=x@; y$99=y@; pos$99(tthin,180);
x$100=x$99-2.5u; y$100=.5[y$99,y$1]; pos$100(tthin,135);
x$1=x@+.5u; top y$1r=iheight; pos$1(tthick,90);
x$2=x$1+u; y$2=y$1-.5u; pos$2(.7tthick,60);
x$3=x$2; y$3=y$2-2u; pos$3(tthin,-90);
x$4=x$4l=x$4r=x$2; y$4=y$2; y$4r=y$4+1; y$4l=y$4-1;
filldraw stroke z$99e{left}...{up}z$100e...{right}z$1e...z$2e...{left}z$3e...{right}z$4e;
penlabels($1,$2,$3,$4);
enddef;

def help_uu =
charspec("9",x_height#,.75x_height#,0);
x181=x101=u; x106=x107=x108=x109=.55w; x111=x112=.8w; x110=w;
x103=x105=.5[x101,x106]; x102=x104=.5[x101,x103];
bot y104r=y106=y107=y111=0; top y105r=y108=top y109r=top y110r=y112=h;
y101=y181=y103=.5y102; y102=.6h;
pos101(.8tthinner,180); pos102(.8tthin,90); pos103(.8tmed,0);
pos104(.8tthin,-90); pos181(.8tthinner,-180); pos105(.8tthick,-270);
pos106(.8tthin,-360); pos107(.8tthin,0); pos108(.8tthin,0);
pos109(.8tthin,90); pos110(.8tthin,90); pos111(.8tthick,0); pos112(.8tthick,0);
if dodraw:
filldraw stroke z101e...{right}z102e...{down}z103e...{left}z104e..z181e...{right}z105e...{down}z106e;
filldraw stroke z107e---z108e; filldraw stroke z109e---z110e;
filldraw stroke z111e---z112e;
fi
enddef;


cmchar "Tamil vii";
beginchar(115,1.5x_height#+u#,body_height#,0);
adjust_fit(0,0);
callchar:="v";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x21r=x10l; x21l=x10r; x21=x10; cloney(21,10); small_iito(21,s,h);
endchar;

cmchar "tamil a";
beginchar(0,2x_height#,x_height#,.6desc_depth#);
adjust_fit(0,0);
x3=.5[x9,x11]; top y1r=h; y5=y12=h; y2=y4=y6=.5[y3,y1]; y3=.8bar_height; x1=x3;
x4=x8=.4[x9,x11]; x2=.6[x9,x11]; x5=.6[x9,x11]; x6=.7[x9,x11]; x7=.8[x9,x11]; x11=x12=x13=w-u;
x9=u; x10=.15[x9,x11]; y8=-.8d; y13=-d; y7=bar_height; y9=.2bar_height; y10=y11=.25bar_height;
z20=z1;
pos1(tthinnest,90); pos2(tthin,0); pos3(tthinnest,-90); pos4(tthinnest,-180);
pos5(hair,90); pos6(curve,0); pos7(tthick,-360); pos8(tthinner,-450);
pos9(tthinnest,-580); pos10(tthinnest,-630); pos11(tthinnest,-630); pos12(tthick,0);
pos13(tthick,0);pos20(tthinner,-270);
filldraw stroke z1e...z2e...z3e...z4e...z20e...{down}z7e...{left}z8e...{up}z9e...z10e---z11e;
filldraw stroke z12e---z13e;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13); endchar;

cmchar "tamil aa";
beginchar(1,2.5x_height#,x_height#,1.4desc_depth#);
adjust_fit(0,0);
x3=.5[x9,x11]; top y1r=h; y5=y12=h; y2=y4=y6=.5[y3,y1]; y3=.8bar_height; x1=x3;
x4=x8=.4[x9,x11]; x2=.6[x9,x11]; x5=.6[x9,x11]; x6=.7[x9,x11]; x7=.8[x9,x11]; x11=x12=x13=2x_height-u;
x9=u; x10=.15[x9,x11]; y8=-.48desc_depth; y13=-.6desc_depth; y7=bar_height; y9=.2bar_height; y10=y11=.25bar_height;
z20=z1; x31=x31r=x31l=x11+u; cloney(31,11);
x32r=x13r; y32=y13; pos32(tthinner,0); x34=w-u; y34=y32; pos34(tthinner,-720);
x33=.5[x32,x34]; y33=0; pos33(tthin,-90); x35=.1[x13,x8]; y35=-1.2d; %bot y35l=-d;
pos35(.8tthick,-810); x36=.2[x8,x13]; y36=-desc_depth; pos36(tthin,-900);
x37=.5[x8,x13]; y37=y13; pos37(tthinner,-990);
pos1(tthinnest,90); pos2(tthin,0); pos3(tthinnest,-90); pos4(tthinnest,-180);
pos5(hair,90); pos6(curve,0); pos7(tthick,-360); pos8(tthinner,-450);
pos9(tthinnest,-580); pos10(tthinnest,-630); pos11(tthinnest,-630); pos12(tthick,0);
pos13(tthick,0);pos20(tthinner,-270);
filldraw stroke z1e...z2e...z3e...z4e...z20e...{down}z7e...{left}z8e...{up}z9e...z10e---z31e{right}
  ...{down}z34e...{left}z35e...{up}z36e...{right}z37e;
filldraw stroke z12e---z13e;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,31,32,33,34,35,36,37); 
endchar;

cmchar "tamil i";
beginchar(2,2x_height#,asc_height#,desc_depth#);
adjust_fit(0,0);
pickup crisp.nib;
x6=.6w; x11=x13=x15=.5w; x16=x17=.9w; x12=.75w;
x5=x7=.55w; x4=.3w; x3=.4w; x2=.5w; x1=.35w;
x8=x10=x14=.15w; x9=.1w; 
y15=h; y5=x_height; y14=y11=.1h; y10=0; y16=.8x_height;
y7=-.1d; y8=y13=y17=-d; y9=y12=-.6d;
y2=y4=y6=.6x_height; y1=.8x_height; y3=.4x_height;
z20=z1; pos20(tthinnest,-200);
z65=.5[z6,z7]; pos65(tthicker,-370);
pos1(tmed,45); pos2(tmed,0); pos3(tthin,-90); pos4(tthinnest,-180);
pos5(tmed,-270); pos6(tthick,-360); pos7(tthicker,-380); pos8(tmed,-450);
pos9(tthinner,-540); pos10(tthinnest,-585); pos11(tthin,-630);
pos12(tthick,-720); pos13(tthin,-810); pos14(tthinner,-880);
pos15(tthick,-990); pos16(tthin,-1060); pos17(tthin,-1060);
filldraw stroke z1e..z2e..z3e..z4e..z20e..z5e..z65e..z8e..z9e..z10e..z11e..z12e..z13e..z14e..z15e...{down}z16e---z17e;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17); endchar;

cmchar "Tamil ii";
beginchar(3,1.125x_height#+2u#,x_height#,0);
adjust_fit(0,0);
callchar:="r";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x7=.5[x1,x6]; x8=x4-.5dot_size;
top y77r=top y88r=bar_height+.375dot_size;
y7=y8=.5[y77r,y77l];
x77=x7; x88=x8; pos7(.75dot_size,0); pos77(.75dot_size,90);
pos8(.75dot_size,0); pos88(.75dot_size,90);
dot(7,77); dot(8,88);
penlabels(7,8); endchar;

cmchar "tamil u";
beginchar(4,2x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="u";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;


cmchar "tamil uu";
beginchar(5,2.8x_height#,x_height#,0);
adjust_fit(0,0);
help_uu;
picture V; transform t;
t=identity shifted(1.4x_height,.25x_height);
V=currentpicture transformed t; currentpicture:=V;
h:=oldh; w:=oldw; d:=oldd;
callchar:="u";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x12=w-u; y12=y11; pos12(tthin,-290);
filldraw stroke z11e--z12e;
endchar;

cmchar "tamil e";
beginchar(6,1.6x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="e";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;

cmchar "tamil ai";
beginchar(7,1.75x_height#,x_height#,.8x_height#);
adjust_fit(0,0);
pickup crisp.nib;
y7=y10=h; y4=y8=y9=.2h; y15=y19=-d; y12=.9bar_height;
y21=.25h; y13=-.2d; y14=-.6d; y16=-.8d; y17=y18=-.3d; y20=-.2d;
y1=y3=.4h; y2=.7h; y11=.75h; y85=0;
%
x14=x1=u; x11=x12=w-u; x8=x9=x17=x18=.55[x1,x11]; x85=x8;
x7=.6[x1,x8]; x10=.5[x9,x11]; x3=.6[x1,x8]; x2=x4=.5[x1,x3];
x13=x2; x15=x7-.7u; x16=x17; x19=.5[x18,x11]; x20=.95x11; x21=.9x11;
%
x77=x8; y77=.6h; z99=z77;
pos77(tthick,-360); pos99(tthick,0);
pos1(tthinner,180); pos2(tmed,90); pos3(tthick,0); pos4(tmed,-90);
z55=z1; pos55(tthinner,-180);
%pos6 ?
pos7(tthick,-270); pos8(tthick,-405);
pos9(tthick,-45); pos10(tthin,-90); pos11(tthick,-180); pos12(tthick,-200);
pos21(tthin,-240); pos13(tthin,-240); pos14(tmed,-180); 
pos15(tthick,-90); pos16(tthick,-45); pos17(tthick,-45);
pos18(tthick,-45); pos19(tthick,90); pos20(tmed,225);
pos85(tthin,-270);
%pickup pencircle scaled .2pt
filldraw stroke z1e..z2e{right}..z3e{down}..z4e{left}..z55e...{right}z7e...{down}z77e---z8e;
%pickup crisp.nib
%filldraw stroke z9e---z99e{up}...z10e{right}...{down}z12e..z21e---z13e..z14e{down}...z15e{right}...z16e...{up}z17e;
filldraw stroke z9e---z99e{up}...z10e{right}...{down}z12e...{left}z85e{left}..z14e{down}...z15e{right}...z16e...{up}z17e;
filldraw stroke z18e{down}...z19e{right}...z20e;
%pickup pencircle scaled .2pt
%draw stroke z18e{down}...z19e{right}...z20e;
penlabels(1,2,3,4,55,7,8,9,10,11,12,21,13,14,15,16,17,19,20); endchar;
   
cmchar "Tamil k";
beginchar(8,1.5x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="k";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;

cmchar "Tamil o";
beginchar(9,1.6x_height#,x_height#,.8x_height#);
adjust_fit(0,0);
x1=x5=x16=u; x7=rt x12r=rt x13r=w-u;
x6=.5[x1,x7]; x15=.5[x16,x12]; x17=.1[x16,x12]; x14=.85[x16,x12];
x11=x9; x8=.4[x9,x7]; x9=x10=.7[x1,x7];
x3=.3[x1,x7]; x2=x4=.5[x1,x3];
top y6r=h; bot y15r=-d;
y2=.5h; y1=y5=y3=.5y2;
bot y4r=bot y8r=0;
y9=y10=y2; y7=.3h; y11=-.1d; y12=y13=-.3d;
y14=-.6d; y16=-.5d; y17=-.2d;
pos1(tthin,180); pos2(tthin,90); pos3(tthick,0); pos4(tthin,-90);
pos5(tthin,-180); pos6(tmed,-270); pos7(tthick,-360); pos8(tmed,-450);
pos9(tthin,-540); pos10(tthin,0); pos11(tmed,5); pos12(tthin,10);
pos13(tthick,10); pos14(tthick,0); pos15(tmed,-90); pos16(tthinner,-180);
pos17(tmed,-250);
filldraw stroke z1e...{right}z2e...{down}z3e...{left}z4e...z5e...{right}z6e
  ...{down}z7e...{left}z8e...z9e;
% pickup pencircle scaled .2pt;
filldraw stroke z10e..z11e...{right}z12e;
filldraw stroke z13e...z14e...{left}z15e...{up}z16e...z17e;
penlabels(1,2,3,4,6,7,8,9,11,12,14,15,16,17);
endchar;


cmchar "Tamil ki";
beginchar(10,1.8x_height#,asc_height#,0);
adjust_fit(0,0);
callchar:="k";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
z45=z21; x46=x3; top y46r=h; x47=x48=w-u; y47=y4; y48=0;
pos45(tthinnest,270); pos46(tthick,90); pos47(tthinner,0);
x48l=x48-.5; x48r=x48+.5; y48l=y48r=0;
filldraw stroke z45e{left}...{right}z46e...z47e{down}---z48e;
penlabels(46,47,48);
endchar;

cmchar "Tamil kii";
beginchar(11,1.5x_height#,body_height#,0);
adjust_fit(0,0);
callchar:="k";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
z45=z21; x46=x3; top y46r=h; 
pos45(tthinnest,270); pos46(tmed,90); 
x47=.6[x3,x4]; y47=.9[y3,y46]; x48=x4; y48=.5[y3,y46];
y49=.3[y3,y46]; x49=x47; x50=.2[x3,x4]; y50=y48; z51=z47;
pos47(.7tmed,45); pos48(tthinner,0); pos49(tthinnest,-90);
pos50(tthinnest,-180); pos51(tthinnest,-225);
filldraw stroke z45e{left}...{right}z46e...z47e...{down}z48e...{left}z49e...{up}z50e...z51e;
penlabels(46,47,48,49,50);
endchar;

cmchar "Tamil ku";
beginchar(12,2x_height#,asc_height#,desc_depth#);
adjust_fit(0,0);
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x45=x5; bot y45r=-d; pos45(tthick,-450);
x46=x46r=x46l=x2; y46=y2; y46r=y46+1; y46l=y46-1;
y26=y25; y26l=y25l; y26r=y25r; x26=x26l=x26r=.7[x3,x4];
x47=-.25x_height; y47=y8; pos47(tthin,-540);
filldraw stroke z25e---z26e{right}...z45e...{up}z47e...{right}z46e;
picture V; transform t; t=identity shifted(.4x_height,0);
V=currentpicture transformed t; currentpicture:=V;
penlabels(26,45,46,47);
endchar;

cmchar "Tamil kuu";
beginchar(13,2.1x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
cloney(26,25); x26=x26l=x26r=.6[x3,x4];
x28=x4; y28=0; x30=x28+(x4-x3); y30=0; top y29l=bar_height; x29=.5[x28,x30];
x31=x30; bot y31l=0; pos31(tthin,90); x32=w-u; bot y32l=0; pos32(tthin,90); 
pos28(tthin,0); pos29(tthinner,-90); pos30(tthin,-180);
filldraw stroke z25e--z26e{right}...{down}z28e;
filldraw stroke z28e{up}...{right}z29e...{down}z30e;
filldraw stroke z31e--z32e;
penlabels(26,28,29,30,31); endchar;

cmchar "Tamil long o";
beginchar(14,1.6x_height#,x_height#,.8x_height#);
adjust_fit(0,0);
x1=x5=x16=u; x7=rt x12r=rt x13r=w-u;
x6=.5[x1,x7]; x15=.5[x16,x12]; x17=.25[x16,x12]; x14=.85[x16,x12];
x11=x9; x8=.4[x9,x7]; x9=x10=.7[x1,x7];
x3=.3[x1,x7]; x2=x4=.5[x1,x3];
top y6r=h; bot y15r=-d;
y2=.5h; y1=y5=y3=.5y2;
bot y4r=bot y8r=0;
y9=y10=y2; y7=.3h; y11=-.1d; y12=y13=-.3d;
y14=-.6d; y16=-.5d; y17=-.2d;
x18=.4[x16,x12]; y18=-.5d; pos18(tthinner,-360);
pos1(tthin,180); pos2(tthin,90); pos3(tthick,0); pos4(tthin,-90);
pos5(tthin,-180); pos6(tmed,-270); pos7(tthick,-360); pos8(tmed,-450);
pos9(tthin,-540); pos10(tthin,0); pos11(tmed,5); pos12(tthin,10);
pos13(tthick,10); pos14(tthick,0); pos15(tmed,-90); pos16(tthinner,-180);
pos17(tmed,-250);
filldraw stroke z1e...{right}z2e...{down}z3e...{left}z4e...z5e...{right}z6e
  ...{down}z7e...{left}z8e...z9e;
% pickup pencircle scaled .2pt;
filldraw stroke z10e..z11e...{right}z12e;
filldraw stroke z13e...z14e...{left}z15e...{up}z16e...{right}z17e...{down}z18e;
penlabels(1,2,3,4,6,7,8,9,11,12,14,15,16,17,18);
endchar;

cmchar "tamil long e";
beginchar(15,1.6x_height#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="e";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x10=x4; bot y10r=-d; pos10(tthick,-90);
x11=x9; bot y11r=0; pos11(tthick,-90);
filldraw stroke z11e--z10e;
penlabels(10);
endchar;

cmchar "tamil ng";
beginchar(16,1.5x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="g";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;

cmchar "Tamil ai matra";
beginchar(17,2x_height#,x_height#,0);
adjust_fit(0,0);
w:=3x_height;
x1=x81=u; x6=.6x10; x8=.6[x1,x6]; x7=.5[x8,x6];
x5=.5[x1,x6]; x3=.5[x1,x8]; x4=x2=.5[x1,x3];
x9=.5[x8,x10]; x10=.7w; x12=.6[x8,x10]; x11=.5[x12,x10];
top y5r=top y9r=h;
bot y4r=bot y7r=bot y11r=0;
y2=y8=y6=y12=y10=.5h;
y81=y1=y3=.5y2;
pos1(tthinner,180); pos2(tthin,90); pos3(tthick,0); pos4(tthin,-90);
pos81(tthinner,-180); pos5(tmed,-270); pos6(tthicker,-360);
pos7(tmed,-450); pos8(tthin,-540); pos9(tmed,-630); pos10(tthicker,-720);
pos11(tmed,-810); pos12(tthin,-900); 
x30=x11; y30=y10; x31=x12; bot y31r=0; pos30(tthick,-720); pos31(tmed,-790);
filldraw stroke z1e...{right}z2e...{down}z3e...{left}z4e..z81e...{right}z5e
  ...{down}z6e...{left}z7e...{up}z8e...{right}z9e...{down}z30e...z31e;
penlabels(1,2,3,4,5,6,7,8,9,30,31);
endchar;

cmchar "tamil ngi";
beginchar(18,1.85x_height#,asc_height#,0);
adjust_fit(0,0);
callchar:="g";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x40=x13; y40=y13; pos40(tthin,0); x41=x40; top y41l=h; pos41(tthick,-90);
x50=x40-2u; y50=.5[y40,y41]; pos50(tmed,-30);
x42=w-u; y42=x_height; x43=x42; y43=y43l=y43r=0; pos42(tthin,-180);
x43r=x43-.6; x43l=x43+.6;
filldraw stroke z40e{left}...{right}z41e...{down}z42e--z43e;
penlabels(41,42,43,44);
endchar;



cmchar "tamil ngii";
beginchar(19,1.75x_height#,body_height#,0);
adjust_fit(0,0);
callchar:="g";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
small_iito(13,s,h);
endchar;

cmchar "tamil ngu";
beginchar(20,1.5x_height#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="g";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
clonex(40,12); y40=y40l=y40r=-d; 
filldraw stroke z12e--z40e;
endchar;

cmchar "tamil nguu";
beginchar(21,1.85x_height#,x_height#,1.5desc_depth#);
adjust_fit(0,0);
callchar:="g";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
clonex(40,12); y40=y40l=y40r=-.5d; 
filldraw stroke z12e--z40e;
x22=w-u; y22=.5y40; x21=.6[x12,x22]; y21=.25d;
pos21(tthinner,-90); pos22(tthin,-180);
x23=.7[x1,x12]; bot y23l=-d; pos23(tthick,-270);
x24=x2; y24=-.5d; pos24(tthin,-360); x25=x7; y25=-.25d; pos25(tthinner,-450);
x26=.5[x1,x12]; y26=-.5d; pos26(tthinnest,-540);
filldraw stroke z40e{up}...{right}z21e...{down}z22e...{left}z23e...{up}z24e
  ...{right}z25e...{down}z26e;
penlabels(40,21,22,23,24,25,26);
endchar;

cmchar "tamil e matra";
beginchar(22,1.4x_height#,body_height#,0);
adjust_fit(0,0);
x1=x3=x5=.6w; x2=x1-1.5u; x4=x1+1.5u; top y3l=bar_height;
pos3(1.2tthin,-90); y2=y4=.5[y1,y3]; bot y1l=.25u; pos1(tthin,90);
pos2(tthin,0); pos4(tmed,-180); y5=y1; pos5(tthin,-270);
x6=u; y6=.5h; pos6(tthinner,-360); top y7l=h; x7=.5w; pos7(1.2tmed,-450);
x8=w-u; y8=x_height; pos8(tthin,-540); x9=x8; y9=0; x9r=x9-.6; x9l=x9+.6;
y9r=y9l=y9; x75=x7; bot y75l=0; pos75(tthin,-280);
filldraw stroke z1e...{up}z2e...{right}z3e...{down}z4e...z5e...{left}z75e
  ...{up}z6e...{right}z7e...z8e--z9e;
penlabels(1,2,3,4,5,75,6,7,8,9);
endchar;

cmchar "tamil long e matra";
beginchar(23,1.4x_height#,body_height#,0);
adjust_fit(0,0);
x1=x3=x5=x10=x8=x12=w-2.5u; x2=x11=x1-1.5u; x4=x9=x1+1.5u; top y3l=bar_height;
pos3(1.2tthin,-90); y2=y4=.5[y1,y3]; bot y1l=.25u; pos1(tthin,90);
pos2(tthin,0); pos4(tmed,-180); y5=y1; pos5(tthin,-270);
x6=u; y6=.5h; pos6(tthinner,-360); top y7l=h; x7=.55w; pos7(1.2tmed,-450);
x75=x7; bot y75l=0; pos75(tthin,-280);
top y8l=h-.25u; pos8(.8tmed,-460); y9=y11=.5[y8,y10]; y10=y8-y3+y1;
y12=y8; pos9(tthin,-540); pos10(tthin,-630); pos11(tthinner,-720);
pos12(tthinnest,-810);
filldraw stroke z1e...{up}z2e...{right}z3e...{down}z4e...z5e...{left}z75e
  ...{up}z6e...{right}z7e...z8e...{down}z9e...{left}z10e...{up}z11e...z12e;
penlabels(1,2,3,4,5,75,6,7,8,9,10,11,12);
endchar;

cmchar "Tamil c";
beginchar(24,1.5x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
y26=y25; y26l=y25l; y26r=y25r; x26=x26l=x26r=w-u; 
filldraw stroke z25e---z26e;
penlabels(26);
endchar;

cmchar "Tamil ci";
beginchar(26,1.7x_height#,asc_height#,0);
adjust_fit(0,0);
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
y26=y25; y26l=y25l; y26r=y25r; x26=x26l=x26r=w-u-.2x_height; 
filldraw stroke z25e---z26e;
z45=z21; x46=x3; top y46r=h; x47=x48=w-u; y47=y4; y48=0;
pos45(tthinnest,270); pos46(tthick,90); pos47(tthinner,0);
x48l=x48-.5; x48r=x48+.5; y48l=y48r=0;
filldraw stroke z45e{left}...{right}z46e...z47e{down}---z48e;
penlabels(26,46,47,48);
endchar;

cmchar "Tamil cii";
beginchar(27,1.6x_height#,body_height#,0);
adjust_fit(0,0);
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
y26=y25; y26l=y25l; y26r=y25r; x26=x26l=x26r=w-u-.1x_height; 
filldraw stroke z25e---z26e;
z45=z21; x46=x3; top y46r=h; 
pos45(tthinnest,270); pos46(tmed,90); 
x47=.6[x3,x4]; y47=.9[y3,y46]; x48=x4; y48=.5[y3,y46];
y49=.3[y3,y46]; x49=x47; x50=.2[x3,x4]; y50=y48; z51=z47;
pos47(.7tmed,45); pos48(tthinner,0); pos49(tthinnest,-90);
pos50(tthinnest,-180); pos51(tthinnest,-225);
filldraw stroke z45e{left}...{right}z46e...z47e...{down}z48e...{left}z49e...{up}z50e...z51e;
penlabels(26,46,47,48,49,50);
endchar;

cmchar "Tamil cu";
beginchar(28,1.65x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
y26=y25; y26l=y25l; y26r=y25r; x26=x26l=x26r=w-u; 
filldraw stroke z25e---z26e;
x27=x28=.5[x25,x26]; y27=y26; y28=0; pos27(tmed,0); pos28(tmed,0);
filldraw stroke z27e--z28e;
penlabels(26,27,28);
endchar;

cmchar "Tamil cuu";
beginchar(29,2x_height#,x_height#,desc_depth#);
adjust_fit(0,0);
w:=1.65x_height;
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
y26=y25; y26l=y25l; y26r=y25r; x26=x26l=x26r=w-u; 
filldraw stroke z25e---z26e;
x27=x28=.5[x25,x26]; y27=y26; y28=0; pos27(tmed,0); pos28(tmed,0);
filldraw stroke z27e--z28e;
x40=x6; bot y40r=-d; pos40(tthick,-90);
x41=-.25x_height; y41=y8; x42=x42l=x42r=x2; y42=y2; y42r=y42+.6; y42l=y42-.6;
pos41(tthinner,-180);
x48=x28; y48=0; pos48(tthinner,180); x49=w-u; y49=0; pos49(tthin,0);
filldraw stroke z48e{up}...{down}z49e...{left}z40e...{up}z41e...{right}z42e;
picture V; transform t; t=identity shifted(.35x_height,0);
V=currentpicture transformed t; currentpicture:=V;
penlabels(26,27,28,40,41);
endchar;

cmchar "tamil ny";
beginchar(32,2x_height#+2u#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
w:=1.7x_height;
callchar:="e";
tspecs;
h:=oldh; w:=oldw; d:=oldd; x99=x9; y99=0; pos99(tthin,0);
x11=x9+4u; x10=.5[x9,x11]; top y10l=bar_height; pos10(tmed,-90); 
y11=.2bar_height; pos11(tthin,-180); x12=.5[x4,x9]; bot y12l=-d; pos12(tthick,-270);
x13=-.2x_height; y13=.3bar_height; pos13(tthin,-360); x14=x5-.75u; y14=y5; pos14(tthinner,-400);
filldraw stroke z99e{up}...{right}z10e...{down}z11e...{left}z12e...{up}z13e...z14e;
picture V; transform t; t=identity shifted(.3x_height,0);
V=currentpicture transformed t; currentpicture:=V;
endchar;

cmchar "tamil nyi";
beginchar(34,2.2x_height#+2u#,asc_height#,1.2desc_depth#);
adjust_fit(0,0);
w:=1.7x_height;
callchar:="e";
tspecs;
h:=oldh; w:=oldw; d:=oldd; x99=x9; y99=0; pos99(tthin,0);
x11=x9+4u; x10=.5[x9,x11]; top y10l=bar_height; pos10(tmed,-90); 
y11=.2bar_height; pos11(tthin,-180); x12=.5[x4,x9]; bot y12l=-d; pos12(tthick,-270);
x13=-.2x_height; y13=.3bar_height; pos13(tthin,-360); x14=x5-.75u; y14=y5; pos14(tthinner,-400);
filldraw stroke z99e{up}...{right}z10e...{down}z11e...{left}z12e...{up}z13e...z14e;
x30=x8; y30=x_height; pos30(tthin,0); x31=x30; top y31l=h; pos31(tthick,-90);
x33=x32=x11+2u; y32=x_height; y33=y33l=y33r=0; pos32(tthin,-180); x33l=x33+.6;
x33r=x33-.6; filldraw stroke z30e{left}...{right}z31e...{down}z32e--z33e;
picture V; transform t; t=identity shifted(.3x_height,0);
V=currentpicture transformed t; currentpicture:=V;
endchar;

cmchar "tamil nyii";
beginchar(35,2x_height#+2u#,body_height#,1.2desc_depth#);
adjust_fit(0,0);
w:=1.7x_height;
callchar:="e";
tspecs;
h:=oldh; w:=oldw; d:=oldd; x99=x9; y99=0; pos99(tthin,0);
x11=x9+4u; x10=.5[x9,x11]; top y10l=bar_height; pos10(tmed,-90); 
y11=.2bar_height; pos11(tthin,-180); x12=.5[x4,x9]; bot y12l=-d; pos12(tthick,-270);
x13=-.2x_height; y13=.3bar_height; pos13(tthin,-360); x14=x5-.75u; y14=y5; pos14(tthinner,-400);
filldraw stroke z99e{up}...{right}z10e...{down}z11e...{left}z12e...{up}z13e...z14e;
small_iito(8,s,h);
picture V; transform t; t=identity shifted(.3x_height,0);
V=currentpicture transformed t; currentpicture:=V;
endchar;

cmchar "tamil nyu";
beginchar(36,2x_height#+u#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
w:=1.7x_height;
callchar:="e";
tspecs;
h:=oldh; w:=oldw; d:=oldd; x99=x9; y99=0; pos99(tthin,0);
x11=x9+4u; x10=.5[x9,x11]; top y10l=bar_height; pos10(tmed,-90); 
y11=.2bar_height; pos11(tthin,-180); x12=.5[x4,x9];  y12=-d; pos12(tthick,-270);
x13=0; y13=-.3d; pos13(tthin,-360); x14=x13+.2u; y14=y13+.2u; pos14(tthin,-450);
x15=x11+1.5u; y15=y14; pos15(tthin,-450); 
x16=x15; y16=y15r; pos16(tthick,0); x17=x16; y17=x_height; pos17(tthick,0);
filldraw stroke z99e{up}...{right}z10e...{down}z11e...{left}z12e...{up}z13e...{right}z14e--z15e;
filldraw stroke z16e--z17e;
penlabels(10,11,12,13,14,15,16,17);
endchar;


cmchar "tamil nyuu";
beginchar(37,2.4x_height#+2u#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
w:=1.7x_height;
callchar:="e";
tspecs;
h:=oldh; w:=oldw; d:=oldd; x99=x9; y99=0; pos99(tthin,0);
x11=x9+4u; x10=.5[x9,x11]; top y10l=bar_height; pos10(tmed,-90); 
y11=.2bar_height; pos11(tthin,-180); x12=.5[x4,x9]; y12=-d; pos12(tthick,-270);
x13=0; y13=-.3d; pos13(tthin,-360); x14=x13+.2u; y14=y13+.2u; pos14(tthin,-450);
x15=x11+2u; y15=y14; pos15(tthin,-450); 
x16=x15; y16=y15r; pos16(tthin,0); x17=x16; y17=x_height; pos17(tthin,0);
filldraw stroke z99e{up}...{right}z10e...{down}z11e...{left}z12e...{up}z13e...{right}z14e--z15e;
filldraw stroke z16e--z17e;
w:=2.4x_height+2u;
top y18r=y17; x18=x17l; pos18(tmed,90); y19=y18; x19=w-u; x40r=x19-u;
y40=y17; x21=x40; y21=0; pos19(tmed,90); pos40(tmed,0); pos21(tmed,0);
filldraw stroke z18e--z19e; filldraw stroke z40e--z21e;
penlabels(10,11,12,13,14,15,16,17,18,19,40,21);
endchar;

cmchar "Tamil t dot";
beginchar(40,1.75x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="T";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;

cmchar "Tamil t dot i";
beginchar(42,1.75x_height#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="T";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x5=.7[x3,x4]; bot y5r=0; pos5(tthin,270); x6=x5-1.5u; top y6r=h; pos6(tthick,90);
x7=x5; y7=bar_height; pos7(tthin,0); x8=x7; y8=-d; pos8(tthinnest,0);
x40=x1; y40=.6[y1,y2];
filldraw stroke z5e{z40-z5}...{right}z6e...{down}z7e--z8e;
penlabels(5,6,7,8); endchar;

cmchar "Tamil t dot ii";
beginchar(43,1.75x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="T";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x5=.7[x3,x4]; y5=0; pos5(tmed,180);
small_iito(5,s,x_height);
penlabels(5,6,7,8); endchar;

cmchar "tamil t dot u";
beginchar(44,1.6x_height#,body_height#,desc_depth#);
adjust_fit(0,0);
x1=x2=.45w; y1=x_height; y2=0; pos1(tmed,0); pos2(tmed,0);
x3r=x2r; y3=y2; pos3(tthinner,0); x5l=w-2.5u; y5=.5[y4,y6]; pos5(tthinner,-180);
x4=.5[x3,x5]; y4=bar_height; pos4(tthin,-90);
x6=x3; bot y6l=-d; pos6(tthick,-270); x7=u; y7=.5[y6,y8]; pos7(tthinner,-360);
x8=x1+u; top y8l=h; pos8(tmed,-450); x9=x10=w-u; y9=y1; pos9(tthin,-540);
y10=y10r=y10l=0; x10l=x10+.6; x10r=x10-.6;
filldraw stroke z1e--z2e;
filldraw stroke z3e{up}...{right}z4e...{down}z5e...{left}z6e...{up}z7e...{right}z8e...{down}z9e--z10e;
penlabels(1,2,4,5,6,7,8,9,10);
endchar;

cmchar "tamil t dot uu";
beginchar(45,1.6x_height#,body_height#,desc_depth#);
adjust_fit(0,0);
x1=x2=.45w; y1=x_height; y2=0; pos1(tmed,0); pos2(tmed,0);
x3r=x2r; y3=y2; pos3(tthinner,0); x5l=w-2.5u; y5=.5[y4,y6]; pos5(tthinner,-180);
x4=.5[x3,x5]; y4=bar_height; pos4(tthin,-90);
x6=x3; bot y6l=-d; pos6(tthick,-270); x7=u; y7=.5[y6,y8]; pos7(tthinner,-360);
x8=x1+u; top y8l=h; pos8(tmed,-450); x9=w-3.5u; y9=asc_height-.25u;
x10=x9+1.5u; y10=.5[y9,y11]; x11=x9; y11=y9-4u; x12=x9-1.5u; y12=y10;
x13=x9; y13=y9; pos9(tthin,-470); pos10(tthin,-540); pos11(tthinner,-630);
pos12(tthinnest,-720); x13l=x13r=x13; y13l=y13+.6; y13r=y13-.6;
filldraw stroke z1e--z2e;
filldraw stroke z3e{up}...{right}z4e...{down}z5e...{left}z6e...{up}z7e...{right}z8e
  ...z9e...{down}z10e...{left}z11e...{up}z12e...{right}z13e;
penlabels(1,2,4,5,6,7,8,9,10,11,12,13);
endchar;

cmchar "Tamil n dot";
beginchar(48,2.8x_height#+u#,x_height#,0);
adjust_fit(0,0);
callchar:="1";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
filldraw stroke z13e---z14e;
filldraw stroke z15e---z16e;
endchar;

cmchar "Tamil n.aa";
beginchar(49,2.8x_height#+3u#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
callchar:="1";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x21=x16; y21=y6;
y22=-d; x22=.4[x1,x21];
x23=x1; y23=.8x_height;
pos21(tthick,-1080); pos22(tthin,-1170);
y23l+.5=y23r-.5=y23; x23l=x23; x23r=x23-1;
filldraw stroke z13e{right}...{down}z21e...{left}z22e...{right}z23e;
picture V; transform t; t=identity shifted(3u,0);
V=currentpicture transformed t; currentpicture:=V;
%penlabels(21,22,23);
endchar;

cmchar "Tamil n.i";
beginchar(50,3x_height#+u#,asc_height#,0);
adjust_fit(0,0);
callchar:="1";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
filldraw stroke z13e---z14e;
filldraw stroke z15e---z16e;
x17=.5[x13,x14]; top y17l=x_height; x18=x17; top y18r=h;
pos17(tthin,270); pos18(tthick,90);
x20=x19=x14+u; y19=x_height; y20=y20r=y20l=0; 
pos19(tthin,0); x20l=x20-.6; x20r=x20+.6;
filldraw stroke z17e{left}...{right}z18e...{down}z19e--z20e;
penlabels(17,18,19,20);
endchar;

cmchar "Tamil n.ii";
beginchar(51,3x_height#,body_height#,0);
adjust_fit(0,0);
callchar:="1";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
filldraw stroke z13e---z14e;
filldraw stroke z15e---z16e;
x17=.5[x13,x14]; top y17l=x_height; pos17(tthin,270); 
small_iito(17,s,h);
penlabels(17);
endchar;

cmchar "Tamil n.u";
beginchar(52,2.8x_height#+3u#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
callchar:="1";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x21=x16+u; y21=y6;
y22=-d; x22=.6[x1,x21];
x23=x1; y23=-.3d; x24=x23+.2u; y24=y23+.2u; x25=w-u; y25=y24;
x27r=x26r=x25; y27=x_height; y26=y25r;
pos21(tthin,-1080); pos22(tthick,-1170);
pos23(tthin,-1260); pos24(tthin,-1350); pos25(tthin,-1350);
filldraw stroke z13e{right}...{down}z21e...{left}z22e...{up}z23e...{right}z24e--z25e;
pos26(tmed,0); pos27(tmed,0); filldraw stroke z26e--z27e;
penlabels(21,22,23,24,25,27);
endchar;

cmchar "Tamil n.uu";
beginchar(53,3.6x_height#+u#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
w:=3x_height+u;
callchar:="1";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x21=x16+u; y21=y6;
y22=-d; x22=.6[x1,x21];
x23=x1; y23=-.3d; x24=x23+.2u; y24=y23+.2u; x25=w-u; y25=y24;
x27r=x26r=x25; y27=x_height; y26=y25r;
pos21(tthin,-1080); pos22(tthick,-1170);
pos23(tthin,-1260); pos24(tthin,-1350); pos25(tthin,-1350);
filldraw stroke z13e{right}...{down}z21e...{left}z22e...{up}z23e...{right}z24e--z25e;
pos26(tmed,0); pos27(tmed,0); filldraw stroke z26e--z27e;
w:=3.6x_height+u; x28=x27l; top y28r=y27; y29=y28; y30=y27; x29=w-u; x30=x29-2u;
x31=x30; y31=0;
pos28(tthin,90); pos29(tthin,90); pos30(tthin,0); pos31(tthin,0);
filldraw stroke z28e--z29e; filldraw stroke z30e--z31e;
penlabels(21,22,23,24,25,27,28,29,30,31);
endchar;



cmchar "Tamil n dot ai";
beginchar(54,3.8x_height#+u#,asc_height#,0);
adjust_fit(0,0);
callchar:="1";
dodraw:=false;
tspecs;
dodraw:=true;
h:=oldh; w:=oldw; d:=oldd;
aipoints;
z211=.5[z210,z2]; pos211(tthin,135);
filldraw stroke z201e...{down}z202e...{left}z203e...{up}z204e...z205e...{right}z206e
  ...{down}z207e...z208e...{down}z209e...{right}z210e...z211e
  ...{right}z2e...{down}z3e...{left}z4e..z81e...{right}z5e
  ...{down}z6e...{left}z7e...{up}z8e...{right}z9e...{down}z10e
  ...{left}z11e...{up}z12e...{right}z13e---z14e;
filldraw stroke z15e---z16e;
picture V; transform t; t=identity shifted(x_height,0);
V=currentpicture transformed t; currentpicture:=V;
endchar;

cmchar "Tamil t";
beginchar(56,1.7x_height#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
%
% finish with tail for t shape
cloney(61,25); x61r=x61l=x61=.5[x3,x4];
rt x62r=w-u; y62=.5y61; pos62(tthick,0);
y63=y64=-.3d; lft y66l=-d; y65=.4[y64,y66];
x63=x61; x64=x2; x65=u; x66=.7[x65,x64];
pos63(tthin,-90); pos64(tthin,-90); pos65(tthinner,0);
pos66(tthick,45);
%filldraw stroke z25e--z61e{right}...{down}z62e...{left}z63e---z64e{left}...{down}z65e..z66e;
filldraw stroke z66e...{up}z65e...{right}z64e---z63e{right}...{up}z62e...{left}z61e---z25e;
penlabels(45,47,48,61,62,63,64,65,66);
endchar;


cmchar "Tamil ti";
beginchar(58,1.85x_height#,asc_height#,1.2desc_depth#);
adjust_fit(0,0);
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
%
% draw the i loop
z45=z21; x46=x3; top y46r=h; x47=x48=w-u; y47=y4; y48=0;
pos45(tthinnest,270); pos46(tthick,90); pos47(tthinner,0);
x48l=x48-.5; x48r=x48+.5; y48l=y48r=0;
filldraw stroke z45e{left}...{right}z46e...z47e{down}---z48e;
%
% finish with tail for t shape
cloney(61,25); x61r=x61l=x61=.5[x3,x4];
rt x62r=.5[x4,x48]; % note this is based on position of i-loop
y62=.5y61; pos62(tthick,0);
y63=y64=-.3d; lft y66l=-d; y65=.4[y64,y66];
x63=x61; x64=x2; x65=u; x66=.7[x65,x64];
pos63(tthinner,-90); pos64(tthinner,-90); pos65(tthinner,0);
pos66(tthick,45);
%filldraw stroke z25e--z61e{right}...{down}z62e...{left}z63e---z64e{left}...{down}z65e..z66e;
filldraw stroke z66e...{up}z65e...{right}z64e---z63e{right}...{up}z62e...{left}z61e---z25e;
penlabels(45,47,48,61,62,63,64,65,66);
endchar;

cmchar "Tamil tii";
beginchar(59,1.65x_height#,asc_height#,1.2desc_depth#);
adjust_fit(0,0);
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
%
% do the upper loop for ii
z45=z21; x46=x3; top y46r=h; 
pos45(tthinnest,270); pos46(tmed,90); 
x47=.6[x3,x4]; y47=.9[y3,y46]; x48=x4; y48=.5[y3,y46];
y49=.3[y3,y46]; x49=x47; x50=.2[x3,x4]; y50=y48; z51=z47;
pos47(.7tmed,45); pos48(tthinner,0); pos49(tthinnest,-90);
pos50(tthinnest,-180); pos51(tthinnest,-225);
filldraw stroke z45e{left}...{right}z46e...z47e...{down}z48e...{left}z49e...{up}z50e...z51e;
%
% finish with tail for t shape
cloney(61,25); x61r=x61l=x61=.5[x3,x4];
rt x62r=w-u; y62=.5y61; pos62(tthick,0);
y63=y64=-.3d; lft y66l=-d; y65=.4[y64,y66];
x63=x61; x64=x2; x65=u; x66=.7[x65,x64];
pos63(tthin,-90); pos64(tthin,-90); pos65(tthinner,0);
pos66(tthick,45);
%filldraw stroke z25e--z61e{right}...{down}z62e...{left}z63e---z64e{left}...{down}z65e..z66e;
filldraw stroke z66e...{up}z65e...{right}z64e---z63e{right}...{up}z62e...{left}z61e---z25e;
penlabels(45,47,48,49,50,61,62,63,64,65,66);
endchar;

cmchar "Tamil tu";
beginchar(60,2.2x_height#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
w:=w-2u;
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
%
% finish with tail for t shape
cloney(61,25); x61r=x61l=x61=.5[x3,x4];
rt x62r=.35[x4,x90]; y62=.5y61; pos62(tthick,0);
x63=x7; y63=-d; x64=x8-2u; y64=.85[y63,y65]; y66=y65=-.3d;
x65=.25[x64,x8]; x66=w-u;rt x67r=x66; y67=bot y66l; x90=x67; y90=x_height;
pos63(tthin,-90); pos64(tthinner,-180); pos65(tthinner,-270);
pos66(tthinner,-270); pos67(.8tthick,0); pos90(.8tthick,0);
filldraw stroke z25e--z61e{right}...{down}z62e...{left}z63e...{up}z64e
 ...{right}z65e---z66e;
filldraw stroke z67e---z90e;
picture V; transform t; t= identity shifted(2u,0);
V=currentpicture transformed t; currentpicture:=V;
penlabels(61,62,63,64,65,66,90);
endchar;

cmchar "Tamil tuu";
beginchar(61,2.6x_height#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
w:=2.2x_height-2u;
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
%
% finish with tail for t shape
cloney(61,25); x61r=x61l=x61=.5[x3,x4];
rt x62r=.35[x4,x90]; y62=.5y61; pos62(tthick,0);
x63=x7; y63=-d; x64=x8-2u; y64=.85[y63,y65]; y66=y65=-.3d;
x65=.25[x64,x8]; x66=w-u;rt x67r=x66; y67=bot y66l; x90=x67; y90=x_height;
pos63(tthin,-90); pos64(tthinner,-180); pos65(tthinner,-270);
pos66(tthinner,-270); pos67(tthin,0); pos90(tthin,0);
filldraw stroke z25e--z61e{right}...{down}z62e...{left}z63e...{up}z64e
 ...{right}z65e---z66e;
filldraw stroke z67e---z90e;
w:=2.6x_height-2u;
x201=x90l; top y201r=x_height; y202=y201; x202=w-u; pos201(tthin,90);
pos202(tthin,90); x203=.6[x201,x202]; y203=x_height; x204=x203;
y204=0; pos203(tmed,0); pos204(tmed,0);
filldraw stroke z201e--z202e; filldraw stroke z203e--z204e;
picture V; transform t; t= identity shifted(2u,0);
V=currentpicture transformed t; currentpicture:=V;
penlabels(61,62,63,64,65,66,90);
endchar;

cmchar "Tamil n";
beginchar(64,1.5x_height#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
callchar:="n";
tspecs; h:= oldh; w:=oldw; d:=oldd;
%draw the end loop
filldraw stroke z7e{up}...{right}z8e...{down}z9e...{left}z10e...{left}z11e...{down}z12e..z13e;
endchar;

cmchar "Tamil ni";
beginchar(66,1.75x_height#,asc_height#,1.2desc_depth#);
adjust_fit(0,0);
callchar:="n";
tspecs; h:= oldh; w:=oldw; d:=oldd;
%draw the end loop
filldraw stroke z7e{up}...{right}z8e...{down}z9e...{left}z10e...{left}z11e...{down}z12e..z13e;
% draw the i loop
x45=.8[x5,x4]; top y45r=x_height; x46=x45; top y46l=h; x47=x48=w-u; y47=y4; y48=0;
pos45(tthinnest,90); pos46(tthick,-90); pos47(tthinner,-180);
x48l=x48+.65; x48r=x48-.65; y48l=y48r=0;
filldraw stroke z45e{left}...{right}z46e...z47e{down}---z48e;
penlabels(45,46,47,48);
endchar;

cmchar "Tamil nii";
beginchar(67,1.65x_height#,body_height#,1.2desc_depth#);
adjust_fit(0,0);
callchar:="n";
tspecs; h:= oldh; w:=oldw; d:=oldd;
%draw the end loop
filldraw stroke z7e{up}...{right}z8e...{down}z9e...{left}z10e...{left}z11e...{down}z12e..z13e;
x31=x9l; y31=x_height; small_iito(31,s,h);
endchar;

cmchar "Tamil nu";
beginchar(68,1.8x_height#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
callchar:="n";
tspecs; h:= oldh; w:=oldw; d:=oldd;
%draw the end loop
x70=.5[x1,x6]; y70=-d; x72=w-u; y72=y82; rt x73r=x72;
y73=y72; x74=x73; y74=x_height; 
z81=z12; z82=z11; pos70(tthinner,-90); pos81(tthinner,-180);
pos82(tthinner,-270); pos72(tthinner,-270); 
pos73(.8tthick,0); pos74(.8tthick,0); 
filldraw stroke z7e{up}...{right}z8e...{down}z9e...{left}z70e...{up}z81e
  ...{right}z82e---z72e;
filldraw stroke z73e--z74e;
penlabels(70,72,73,74,81,82);
endchar;

cmchar "Tamil nuu";
beginchar(69,2.1x_height#,x_height#,.5x_height#);
adjust_fit(0,0);
callchar:="n";
tspecs; h:= oldh; w:=oldw; d:=oldd;
%draw the end loop
x70=.5[x1,x6]; y70=-d; x72=w-3u; y72=y82; rt x73r=x72;
y73=y72; x74=x73; y74=x_height; 
z81=z12; z82=z11; pos70(tthinner,-90); pos81(tthinner,-180);
pos82(tthinner,-270); pos72(tthinner,-270); 
pos73(tthinner,0); pos74(tthinner,0); 
filldraw stroke z7e{up}...{right}z8e...{down}z9e...{left}z70e...{up}z81e
  ...{right}z82e---z72e;
filldraw stroke z73e--z74e;
top y90r=x_height; x90=lft x73l; x91=w-u; y91=y90;
pos90(tthinner,90); pos91(tthinner,90); filldraw stroke z90e--z91e;
rt x92r=x91; y92=x_height; y93=0; x93=x92;
pos92(.8tthick,0); pos93(.8tthick,0); filldraw stroke z92e--z93e;
penlabels(70,72,73,74,81,82,90,91,92,93);
endchar;

cmchar "Tamil p";
beginchar(72,1.4x_height#+u#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="p";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;

cmchar "Tamil pi";
beginchar(74,1.5x_height#+2u#,asc_height#,0);
adjust_fit(0,0);
callchar:="p";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x7=x6; top y7l=h; pos7(tthick,-90); x8=x9=w-u; y8=x_height; y9=y9r=y9l=0;
x9r=x9-.6; x9l=x9+.6; pos8(tthin,-180);
filldraw stroke z6e{left}...{right}z7e...{down}z8e--z9e;
penlabels(7,8,9); endchar;

cmchar "Tamil pii";
beginchar(75,1.4x_height#+u#,body_height#,0);
adjust_fit(0,0);
callchar:="p";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x7=x6; x7r=x6l; x7l=x6r; cloney(7,6);
small_iito(7,s,h);
endchar;

cmchar "Tamil pu";
beginchar(76,1.4x_height#+u#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="p";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
clonex(7,5); y7r=y7l=y7=-d; filldraw stroke z5e--z7e;
endchar;

cmchar "Tamil puu";
beginchar(77,1.4x_height#+4u#,x_height#,1.4desc_depth#);
adjust_fit(0,0);
callchar:="p";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
clonex(7,5); y7r=y7l=y7=-.5d; filldraw stroke z5e--z7e;
x8=x7; y8=y7; pos8(tthin,0); x10=w-u; y10=.5[y9,y11]; pos10(tthin,-180);
x9=.5[x8,x10]; top y9l=0; pos9(tthin,-90); x11=.5[x1,x10]; y11=-d;
x12=x1; y12=-.4d; x13=.25[x1,x4]; y13=-.2d; x14=.5[x1,x4]; y14=-.45d;
pos11(tthick,-270); pos12(tthin,-360); pos13(tthin,-450); pos14(tthinner,-540);
filldraw stroke z8e{up}...{right}z9e...{down}z10e...{left}z11e...{up}z12e
  ...{right}z13e...{down}z14e;
endchar;

cmchar "Tamil m";
beginchar(80,1.6x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="m";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;

cmchar "Tamil mi";
beginchar(82,1.85x_height#,asc_height#,0);
adjust_fit(0,0);
callchar:="m";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x10=x6; top y10l=h; y11=x_height; x12=x11=w-u; y12=0;
pos10(tthick,-90); pos11(tthin,-180); y12r=y12l=y12;
x12r=x12-.6; x12l=x12+.6;
filldraw stroke z6e{left}...{right}z10e...{down}z11e--z12e;
penlabels(10,11,12); endchar;

cmchar "Tamil mii";
beginchar(83,1.6x_height#,body_height#,0);
adjust_fit(0,0);
callchar:="m";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x10=x6; y10l=x_height; x10r=x10-.6; x10l=x10+.6; y10=y10l-.6; y10r=y10-.6;
small_iito(10,s,h);
endchar;

cmchar "Tamil mu";
beginchar(84,2.2x_height#,x_height#,desc_depth#);
adjust_fit(0,0);
w:=1.6x_height;
callchar:="m";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x9=x8; y9=0; pos9(tthick,-180);
x10=.25[x2,x8]; bot y10l=-d; pos10(tmed,-270);
x11=-.6x_height+u; y11=.5bar_height; x12=x1-.5u; y12l=h;
pos11(tthin,-360); x12l=x12r=x12; y12=y12l-.6; y12r=y12-.6;
filldraw stroke z9e{down}...{left}z10e...{up}z11e...{right}z12e;
picture V; transform t; t=identity shifted(.6x_height,0);
V=currentpicture transformed t; currentpicture:= V;
endchar;


cmchar "Tamil muu";
beginchar(85,2.2x_height#,x_height#,desc_depth#);
adjust_fit(0,0);
w:=1.6x_height;
callchar:="m";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x9=x8; y9=0; pos9(tthick,-180);
x10=.25[x2,x8]; bot y10l=-d; pos10(tmed,-270);
x11=-.6x_height+u; y11=.5bar_height; x12=x1-2.5u; y12l=h;
pos11(tthin,-360); pos12(tthinnest,-450);
x13=x12+u; y14=y12-u; x14=x13-2u; y14=y13;
pos13(tthinnest,-540); x14l=x14-.6; x14r=x14+.6; y14r=y14l=y14;
filldraw stroke z9e{down}...{left}z10e...{up}z11e...{right}z12e...{down}z13e...{z14-z2}z14e;
picture V; transform t; t=identity shifted(.6x_height,0);
V=currentpicture transformed t; currentpicture:= V;
endchar;

cmchar "Tamil y";
beginchar(88,1.5x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="y";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;

cmchar "Tamil yi";
beginchar(90,1.9x_height#,asc_height#,0);
adjust_fit(0,0);
callchar:="y";
tspecs;
h:=oldh; w:=oldw; d:=oldd; 
x20=x9; y20l=h; pos20(tthick,-90); x21=w-u; y21=x_height; pos21(tthin,-180);
x22=x21; y22=y22l=y22r=0; x22r=x22-.6; x22l=x22+.6;
filldraw stroke z9e{left}...{right}z20e...{down}z21e--z22e;
penlabels(20,21,22); endchar;

cmchar "Tamil yii";
beginchar(91,1.65x_height#,body_height#,0);
adjust_fit(0,0);
callchar:="y";
tspecs;
h:=oldh; w:=oldw; d:=oldd; 
x20=x9; y20=y9; pos20(tthin,180);
small_iito(20,s,h);
endchar;

cmchar "Tamil yu";
beginchar(92,1.5x_height#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="y";
tspecs;
h:=oldh; w:=oldw; d:=oldd; 
clonex(20,10); y20=y20l=y20r=-d;
filldraw stroke z10e--z20e;
penlabels(20); endchar;

cmchar "Tamil yuu";
beginchar(93,1.75x_height#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="y";
tspecs;
h:=oldh; w:=oldw; d:=oldd; 
clonex(20,10); y20=y20l=y20r=-.5d;
filldraw stroke z10e--z20e;
x22=w-u; y22=0; x21=.6[x10,x22]; y21=.25d;
pos21(tthinner,-90); pos22(tthin,-180);
x23=.7[x7,x8]; y23=-1.2d; pos23(tthick,-270);
x24=x2; y24=-.5d; pos24(tthin,-360); x25=x7; y25=-.25d; pos25(tthinner,-450);
x26=.5[x7,x8]; y26=-.5d; pos26(tthinnest,-540);
filldraw stroke z20e{up}...{right}z21e...{down}z22e...{left}z23e...{up}z24e
  ...{right}z25e...{down}z26e;
penlabels(20,21,22,23,24,25,26); endchar;

cmchar "Tamil r";
beginchar(96,.9x_height#+2u#,x_height#,.75desc_depth#);
adjust_fit(0,0);
callchar:="r2";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x7=x6; bot y7l=y6; x8=x1; bot y8l=-d;
pos7(tthin,90); pos8(tthin,90);
filldraw stroke z7e--z8e;
penlabels(7,8); endchar;

cmchar "Tamil aa matra";
beginchar(97,.9x_height#+2u#,x_height#,0);
adjust_fit(0,0);
callchar:="r2";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;

cmchar "Tamil ri";
beginchar(98,.9x_height#+3.5u#,asc_height#,0);
adjust_fit(0,0);
callchar:="r2";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x7=.5[x5,x4]; y7=y5; x8=x7; top y8r=h; x9=x10=w-u; y9=x_height; y10=0;
pos7(tthinner,-90); pos8(tthick,90); pos9(tthin,0);
x10l=x10-.6; x10r=x10+.6; y10r=y10l=0;
filldraw stroke z7e{left}...{right}z8e...{down}z9e--z10e;
penlabels(7,8,9,10); endchar;

cmchar "Tamil rii";
beginchar(99,.9x_height#+2.5u#,body_height#,0);
adjust_fit(0,0);
callchar:="r2";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x7=.5[x5,x4]; y7=x_height; pos7(tthinner,-90);
small_iito(7,s,body_height);
penlabels(7,8,9,10); endchar;

cmchar "Tamil ru";
beginchar(100,.9x_height#+7u#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
callchar:="r2";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x7=x6; y7=y6; x8=.7[x5,x4]; top y8l=bar_height; pos8(tthick,-90);
pos7(tthin,0); x9=1.4[x5,x4]; y9=0; pos9(tthin,-180);
x10=.7[x1,x6]; bot y10l=-d; pos10(tthinner,-270); x11=x1-4u; y11=.5bar_height;
pos11(tthinner,-360); x12=x2-u; top y12l=x_height; pos12(tthinnest,-450);
filldraw stroke z7e{up}...{right}z8e...{down}z9e...{left}z10e...{up}z11e...{right}z12e;
picture V; transform t; t=identity shifted(3.75u,0);
V=currentpicture transformed t; currentpicture:= V;
penlabels(7,8,9,10,11,12); endchar;


cmchar "Tamil ruu";
beginchar(101,.9x_height#+9u#,x_height#,1.2desc_depth#);
adjust_fit(0,0);
callchar:="r2";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x7=x6; y7=y6; x8=.7[x5,x4]; top y8l=bar_height; pos8(tthick,-90);
pos7(tthin,0); x9=1.4[x5,x4]; y9=0; pos9(tthin,-180);
x10=.7[x1,x6]; bot y10l=-d; pos10(tthinner,-270); x11=x1-6u; y11=.5bar_height;
pos11(tthinner,-360); x12=.5[x11,x2]; top y12l=x_height; pos12(tthin,-450);
x13=.8[x11,x2]; y13=y12-1.5u; x14=.3[x11,x2]; y14=y12-3u;
pos13(tthin,-540); pos14(tthinner,-600);
filldraw stroke z7e{up}...{right}z8e...{down}z9e...{left}z10e...{up}z11e...{right}z12e
  ...{down}z13e...z14e;
picture V; transform t; t=identity shifted(5.75u,0);
V=currentpicture transformed t; currentpicture:= V;
penlabels(7,8,9,10,11,12); endchar;


cmchar "Tamil l";
beginchar(104,1.4x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="l";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x11=x9; top y11r=h; x12=x11+.5u; y12=y11-.25u; pos11(tthick,90); pos12(.7tthick,60);
x13=x12; y13=x12-2u; pos13(tthin,-90); x14r=x14l=x14=x12; y14=y12; y14r=y14+1; y14l=y14-1;
filldraw stroke z4e...z1e..z2e..z3e..z4e...{up}z81e..z5e...{down}z6e--z7e{down}...{right}z8e...{up}z9e
  ...{left}z10e;
endchar;

cmchar "Tamil li";
beginchar(106,1.6x_height#+2u#,body_height#,0);
adjust_fit(0,0);
callchar:="l";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x11=x10; top y11r=h; pos11(tthick,90); x13=x12=w-u; y12=x_height;
y13r=y13l=y13=0; x13l=x13-.6; x13r=x13+.6; pos12(tthin,0);
filldraw stroke z4e...z1e..z2e..z3e..z4e...{up}z81e..z5e...{down}z6e--z7e{down}...{right}z8e...{up}z9e
  ...{left}z10e...{right}z11e...{down}z12e--z13e;
%filldraw stroke z10e...{right}z11e...{down}z12e--z13e;
penlabels(11,12,13);
endchar;

cmchar "Tamil lii";
beginchar(107,1.5x_height#,asc_height#,0);
adjust_fit(0,0);
callchar:="l";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x11=x9; top y11r=h; x12=x11+.5u; y12=y11-.25u; pos11(tthick,90); pos12(.7tthick,60);
x13=x12; y13=x12-2u; pos13(tthin,-90); x14r=x14l=x14=x12; y14=y12; y14r=y14+1; y14l=y14-1;
filldraw stroke z4e...z1e..z2e..z3e..z4e...{up}z81e..z5e...{down}z6e--z7e{down}...{right}z8e...{up}z9e
  ...{left}z10e...{right}z11e..z12e...{left}z13e...{right}z14e;
%small_iito(10,s,h);
endchar;

cmchar "Tamil lu";
beginchar(108,2x_height#,x_height#,1.4desc_depth#);
adjust_fit(0,0);
callchar:="l";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
filldraw stroke z4e...z1e..z2e..z3e..z4e...{up}z81e..z5e...{down}z6e--z7e{down}...{right}z8e...{up}z9e
  ...{left}z10e;
z21=z8; pos21(tthin,0); x22=.15[x5,x21]; bot y22r=-d; pos22(tthick,-90);
x23=x81-u; y23=-.4d; x24=x23+.5u; y24=y23+.5u; pos23(tthin,-180); pos24(tthin,-270);
x25=w-u; y25=y24; pos25(tthin,-270); x26r=x25; x27=x26; pos26(tmed,0);
y26=y25l; y27=x_height; pos27(tmed,0);
filldraw stroke z21e{down}...{left}z22e...{up}z23e...{right}z24e--z25e;
filldraw stroke z26e--z27e;
penlabels(21,22,23,24,25,26,27);
endchar;

cmchar "Tamil luu";
beginchar(109,2.4x_height#,x_height#,1.4desc_depth#);
adjust_fit(0,0);
w:=1.9x_height;
callchar:="l";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
filldraw stroke z4e...z1e..z2e..z3e..z4e...{up}z81e..z5e...{down}z6e--z7e{down}...{right}z8e...{up}z9e
  ...{left}z10e;
z21=z8; pos21(tthin,0); x22=.15[x5,x21]; bot y22r=-d; pos22(tthick,-90);
x23=x81-u; y23=-.4d; x24=x23+.5u; y24=y23+.5u; pos23(tthin,-180); pos24(tthin,-270);
x25=w-u; y25=y24; pos25(tthin,-270); x26r=x25; x27=x26; pos26(tthin,0);
y26=y25l; y27=x_height; pos27(tthin,0);
filldraw stroke z21e{down}...{left}z22e...{up}z23e...{right}z24e--z25e;
filldraw stroke z26e--z27e;
w:=2.4x_height;
x28=x27l; y28r=y27; y29=y28; x29=w-u; pos28(tthin,90); pos29(tthin,90);
filldraw stroke z28e--z29e;
x31=x30=.65[x28,x29]; y30=y27; y31=0; pos30(tmed,0); pos31(tmed,0);
filldraw stroke z30e--z31e;
penlabels(21,22,23,24,25,26,27,28,29,30);
endchar;

cmchar "Tamil lai";
beginchar(110,2.8x_height#,asc_height#,0);
adjust_fit(0,0);
callchar:="l";
tspecs;
h:=oldh; w:=oldw; d:=oldd; aipoints;
z211=.5[z210,z2]; pos211(tthin,135);
filldraw stroke z201e...{down}z202e...{left}z203e...{up}z204e...z205e...{right}z206e
  ...{down}z207e...z208e...{down}z209e...{right}z210e...z211e
  ...{right}z2e...{down}z3e...{left}z4e...{up}z81e...{right}z5e...{down}z6e--z7e{down}...{right}z8e...{up}z9e...{left}z10e;
picture V; transform t; t=identity shifted(x_height,0);
V=currentpicture transformed t; currentpicture:=V;
endchar;



cmchar "Tamil v";
beginchar(112,1.4x_height#+2u#,x_height#,0);
adjust_fit(0,0);
callchar:="v";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;

cmchar "Tamil vi";
beginchar(114,1.65x_height#+2u#,asc_height#,0);
adjust_fit(0,0);
callchar:="v";
tspecs;
h:=oldh; w:=oldw; d:=oldd; x21r=x10l; x21l=x10r; x21=x10; cloney(21,10);
x11=x10; top y11r=h; pos11(tthick,90); x13=x12=w-u; y12=x_height;
y13r=y13l=y13=0; x13l=x13-.6; x13r=x13+.6; pos12(tthin,0);
filldraw stroke z21e{left}...{right}z11e...{down}z12e--z13e;
penlabels(11,12,13);
endchar;

cmchar "Tamil vii";
beginchar(115,1.5x_height#+u#,body_height#,0);
adjust_fit(0,0);
callchar:="v";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x21r=x10l; x21l=x10r; x21=x10; cloney(21,10); small_iito(21,s,h);
endchar;

cmchar "Tamil vu";
beginchar(116,1.5x_height#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="v";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
clonex(11,9); y11r=y11l=y11=-d; filldraw stroke z9e--z11e;
penlabels(11); endchar;

cmchar "Tamil vuu";
beginchar(117,1.5x_height#+3u#,x_height#,1.4desc_depth#);
adjust_fit(0,0);
callchar:="v";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
clonex(11,9); y11r=y11l=y11=-.5d; filldraw stroke z9e--z11e;
x18=x11; y18=y11; pos18(tthin,0); x20=w-u; y20=.5[y19,y21]; pos20(tthin,-180);
x19=.5[x18,x20]; top y19l=0; pos19(tthin,-90); x21=.5[x1,x20]; y21=-d;
x22=x1; y22=-.4d; x23=.25[x1,x9]; y23=-.2d; x24=.5[x1,x9]; y24=-.45d;
pos21(tthick,-270); pos22(tthin,-360); pos23(tthin,-450); pos24(tthinner,-540);
filldraw stroke z18e{up}...{right}z19e...{down}z20e...{left}z21e...{up}z22e
  ...{right}z23e...{down}z24e;
penlabels(11); endchar;

cmchar "Tamil z";
beginchar(120,1.6x_height#,x_height#,1.4desc_depth#);
adjust_fit(0,0);
callchar:="m";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x9=x9r=x9l=x5; y9=-.3d; y9l=y9+.6;y9r=y9-.6; filldraw stroke z8e{down}...{right}z9e;
x90l=x9l; y90l=y9l; pos90(tthin,-170);
x10=.4[x1,x5]; bot y10r=-d; x11=x1; y11=.3[y9,y10]; pos10(tthick,-90); pos11(tthin,-180);
x12=.25[x2,x5]; y12=.5[y2,y11]; pos12(tthinner,40);
filldraw stroke z9e{left}...{left}z10e...{up}z11e...z12e;
penlabels(9,10,11,12);
endchar;

cmchar "Tamil zi";
beginchar(122,1.9x_height#,asc_height#,1.4desc_depth#);
adjust_fit(0,0);
callchar:="m";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x9=x9r=x9l=x5; y9=-.3d; y9l=y9+.6;y9r=y9-.6; filldraw stroke z8e{down}...{right}z9e;
x90l=x9l; y90l=y9l; pos90(tthin,-170);
x10=.4[x1,x5]; bot y10r=-d; x11=x1; y11=.3[y9,y10]; pos10(tthick,-90); pos11(tthin,-180);
x12=.25[x2,x5]; y12=.5[y2,y11]; pos12(tthinner,40);
filldraw stroke z9e{left}...{left}z10e...{up}z11e...z12e;
x30=x6; top y30l=h; y31=x_height; x32=x31=w-u; y32=0;
pos30(tthick,-90); pos31(tthin,-180); y32r=y32l=y32;
x32r=x32-.6; x32l=x32+.6;
filldraw stroke z6e{left}...{right}z30e...{down}z31e--z32e;
penlabels(9,10,11,12,30,31,32);
endchar;

cmchar "Tamil zii";
beginchar(123,1.6x_height#,body_height#,1.4desc_depth#);
adjust_fit(0,0);
callchar:="m";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x9=x9r=x9l=x5; y9=-.3d; y9l=y9+.6;y9r=y9-.6; filldraw stroke z8e{down}...{right}z9e;
x90l=x9l; y90l=y9l; pos90(tthin,-170);
x10=.4[x1,x5]; bot y10r=-d; x11=x1; y11=.3[y9,y10]; pos10(tthick,-90); pos11(tthin,-180);
x12=.25[x2,x5]; y12=.5[y2,y11]; pos12(tthinner,40);
filldraw stroke z9e{left}...{left}z10e...{up}z11e...z12e;
small_iito(6,s,h);
penlabels(9,10,11,12);
endchar;

cmchar "Tamil zu";
beginchar(124,2.2x_height#,x_height#,1.4desc_depth#);
adjust_fit(0,0);
callchar:="m";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
clonex(88,8); y88r=y88l=y88=-.5d; filldraw stroke z8e--z88e;
x9=x88; y9=y88; pos9(tthin,-360); x99=x4; y99=y9; pos99(tthin,-180);
x10=.25[x2,x8]; bot y10l=-d; pos10(tmed,-270);
x11=-.6x_height+u; y11=.5bar_height; x12=x1-.5u; y12l=h;
pos11(tthin,-360); x12l=x12r=x12; y12=y12l-.6; y12r=y12-.6;
filldraw stroke z9e{up}...{down}z99e...{left}z10e...{up}z11e...{right}z12e;
picture V; transform t; t=identity shifted(.6x_height,0);
V=currentpicture transformed t; currentpicture:= V;
endchar;

cmchar "Tamil zuu";
beginchar(125,2.2x_height#,x_height#,1.4desc_depth#);
adjust_fit(0,0);
callchar:="m";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
clonex(88,8); y88r=y88l=y88=-.5d; filldraw stroke z8e--z88e;
x9=x88; y9=y88; pos9(tthin,-360); x99=x4; y99=y9; pos99(tthin,-180);
x10=.25[x2,x8]; bot y10l=-d; pos10(tmed,-270);
x11=-.6x_height+u; y11=.5bar_height; x12=.5[x11,x1]; top y12l=h;
pos11(tthin,-360); pos12(tthin,-450); x13=.8[x11,x1]; y13=y12-1.5u;
pos13(tthin,-540); x14=.3[x11,x1]; y14=y13-1.5u; pos14(tthinner,-600);
filldraw stroke z9e{up}...{down}z99e...{left}z10e...{up}z11e...{right}z12e
  ...{down}z13e...z14e;
picture V; transform t; t=identity shifted(.6x_height,0);
V=currentpicture transformed t; currentpicture:= V;
endchar;



cmchar "Tamil l dot";
beginchar(128,1.75x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="L";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;

cmchar "Tamil l dot i";
beginchar(130,1.75x_height#+2u#,asc_height#,0);
adjust_fit(0,0);
callchar:="L";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
y13=y12; x13=x12r; x14=x13; top y14r=h; x15=x16=w-u; y15=y12; y16l=y16r=y16=0;
x16l=x16-.6; x16r=x16+.6; pos13(tthin,180); pos14(tthick,90); pos15(tthin,0);
filldraw stroke z13e{left}...{right}z14e...{down}z15e--z16e;
penlabels(14,15,16); endchar;

cmchar "Tamil l dot ii";
beginchar(131,1.75x_height#+u#,body_height#,0);
adjust_fit(0,0);
callchar:="L";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
y13=y12; x13=x12r; 
pos13(tthin,0);
small_iito(13,s,h);
endchar;

cmchar "Tamil l dot u";
beginchar(132,2.05x_height#+3u#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="L";
tspecs;
h:=oldh; w:=2.05x_height; d:=oldd;
x31=x11; y31=0; pos31(tthin,0); x33=w-u; y33=.5[y32,y34]; pos33(tthin,-180);
x32=.5[x31,x33]; y32=bar_height; pos32(tthin,-90);
x35=x1-3u; y35=.5[y1,y2]; pos35(tthin,-360); x34=.5[x35,x33]; bot y34l=-d;
pos34(tthick,-270); x36=x1; y36=y5; x36r=x36+.6; x36l=x36-.6;
y36r=y36-.6; y36l=y36+.6;
filldraw stroke z31e{up}...{right}z32e...{down}z33e...{left}z34e...{up}z35e..{right}z36e;
picture V; transform t; t=identity shifted(3u,0); V=currentpicture transformed t;
currentpicture:=V;
endchar;

cmchar "Tamil l dot uu";
beginchar(133,2.05x_height#+3u#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="L";
tspecs;
h:=oldh; w:=2.05x_height; d:=oldd;
x31=x11; y31=0; pos31(tthin,0); x33=w-u; y33=.5[y32,y34]; pos33(tthin,-180);
x32=.5[x31,x33]; y32=bar_height; pos32(tthin,-90);
x35=x1-3u; y35=.5[y1,y2]; pos35(tthin,-360); x34=.5[x35,x33]; bot y34l=-d;
pos34(tthick,-270); x36=.5[x35,x1]; top y36l=h; pos36(tthin,-450);
x37=.9[x35,x1]; y37=y36-u; pos37(tthin,-540); x38=.3[x35,x1]; y38=y37-u;
pos38(tthinner,-600);
filldraw stroke z31e{up}...{right}z32e...{down}z33e...{left}z34e...{up}z35e..{right}z36e
   ...{down}z37e...z38e;
picture V; transform t; t=identity shifted(3u,0); V=currentpicture transformed t;
currentpicture:=V;
endchar;

cmchar "Tamil l dot ai";
beginchar(134,2.75x_height#,asc_height#,0);
adjust_fit(0,0);
callchar:="L";
dodraw:=false;
tspecs;
dodraw:=true;
h:=oldh; w:=oldw; d:=oldd;
aipoints;
z211=.5[z210,z2]; pos211(tthin,135);
filldraw stroke z201e...{down}z202e...{left}z203e...{up}z204e...z205e...{right}z206e
  ...{down}z207e...z208e...{down}z209e...{right}z210e...z211e
  ...{right}z2e...{down}z3e...{left}z4e...{up}z1e...{right}z5e...{down}z6e;
filldraw stroke z7e---z8e; filldraw stroke z9e---z10e;
filldraw stroke z11e---z12e;
picture V; transform t; t=identity shifted(x_height,0);
V=currentpicture transformed t; currentpicture:=V;
endchar;

cmchar "Tamil alv. r";
beginchar(136,1.3x_height#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="R";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
filldraw stroke z4e{up}...{right}z6e...{down}z7e..z8e..z9e...{down}z12e..z10e;
endchar;

cmchar "Tamil alv. raa";
beginchar(137,1.3x_height#+3u#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="R";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x77=x7; y77=.4[y6,y7]; pos77(tthin,-180);
x7:=x7-u; x7r:=x7r-u; x7l:=x7l-u;
x88=x77; y88=0; pos88(tthin,-180);
x31=x5; bot y31l=-d; x32=-2u; y32=.3bar_height; 
pos31(tthick,-270); pos32(tthin,-360); 
x33=x33r=x33l=x11-.5u; y33=y11; y33l=y33+.6; y33r=y33-.6;
filldraw stroke z4e{up}...{right}z6e...{down}z77e...{down}z7e...{down}z88e...{left}z31e...{up}z32e...z33e;
picture V; transform t; t=identity shifted(3u,0);
V=currentpicture transformed t; currentpicture:=V;
endchar;

cmchar "Tamil alv. ri";
beginchar(138,1.7x_height#,asc_height#,desc_depth#);
adjust_fit(0,0);
callchar:="R";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
filldraw stroke z4e{up}...{right}z6e...{down}z7e..z8e..z9e...{down}z12e..z10e;
x77=x6; top y77r=h; pos77(tthick,-270);
x78=w-u; y78=x_height; x79=x78; y79=y79r=y79l=0;
x79r=x79+.6; x79l=x79-.6; pos78(tthin,-360);
filldraw stroke z6e{left}...{right}z77e...{down}z78e--z79e;
endchar;

cmchar "Tamil alv. rii";
beginchar(139,1.3x_height#,body_height#,desc_depth#);
adjust_fit(0,0);
callchar:="R";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
filldraw stroke z4e{up}...{right}z6e...{down}z7e..z8e..z9e...{down}z12e..z10e;
small_iito(6,s,h);
endchar;

cmchar "Tamil alv. ru";
beginchar(140,1.75x_height#,x_height#,1.4desc_depth#);
adjust_fit(0,0);
callchar:="R";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x77=x7; y77=y7; pos77(tthin,-180);
x31=x5; bot y31l=-d; x32=x1; y32=-.5d;  y33=y32+.5u; x33=x32+.5u; x34=w-u; y34=y33;
pos31(tthick,-270); pos32(tthin,-360); pos33(tthin,-450); pos34(tthin,-450);
filldraw stroke z4e{up}...{right}z6e...{down}z77e...{left}z31e...{up}z32e...{right}z33e--z34e;
x35r=x34; y35=y34r; x36=x35; y36=x_height; pos35(tthick,0); pos36(tthick,0);
filldraw stroke z35e--z36e;
endchar;

cmchar "Tamil alv. ruu";
beginchar(141,2x_height#,x_height#,1.4desc_depth#);
adjust_fit(0,0);
callchar:="R";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x77=x7; y77=y7; pos77(tthin,-180);
x31=x5; bot y31l=-d; x32=x1; y32=-.5d; y33=y32+.5u; x33=x32+.5u; x34=w-3.3u; y34=y33;
pos31(tthick,-270); pos32(tthin,-360); pos33(tthin,-450); pos34(tthin,-450);
filldraw stroke z4e{up}...{right}z6e...{down}z77e...{left}z31e...{up}z32e...{right}z33e--z34e;
x35r=x34; y35=y34r; x36=x35; y36=x_height; pos35(tthin,0); pos36(tthin,0);
filldraw stroke z35e--z36e;
x37=x36l; top y37r=x_height; x38=w-.3u; y38=y37; pos37(tthin,90); pos38(tthin,90);
filldraw stroke z37e--z38e;
x39r=x38-.7u; y39=x_height; x40=x39; y40=0; pos39(tthick,0); pos40(tthick,0);
filldraw stroke z39e--z40e;
endchar;


cmchar "Tamil 2loop n";
beginchar(144,2x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="N";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
filldraw stroke z13e---z14e;
filldraw stroke z15e---z16e;
endchar;

cmchar "Tamil 2loop naa";
beginchar(145,2x_height#+3u#,x_height#,desc_depth#);
adjust_fit(0,0);
w:=2x_height;
callchar:="N";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x21=x16+u; y21=y6;
y22=-d; x22=.4[x1,x21];
x23=x1; y23=.8x_height;
pos21(tthick,-1080); pos22(tthin,-1170);
y23l+.5=y23r-.5=y23; x23l=x23; x23r=x23-1;
filldraw stroke z13e{right}...{down}z21e...{left}z22e...{right}z23e;
picture V; transform t; t=identity shifted(3u,0);
V=currentpicture transformed t; currentpicture:=V;
endchar;



cmchar "Tamil 2loop ni";
beginchar(146,2.25x_height#,asc_height#,0);
adjust_fit(0,0);
callchar:="N";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
filldraw stroke z13e---z14e;
filldraw stroke z15e---z16e;
x21=x16; y21=x_height; pos21(tthin,0); x22=x21; top y22l=h; pos22(tthick,-90);
x23=w-u; y23=x_height; x24=x23; y24=y24l=y24r=0; x24r=x24-.6; x24l=x24+.6;
pos23(tthin,-180);
filldraw stroke z21e{left}...{right}z22e...{down}z23e--z24e;
penlabels(22,23,24);
endchar;

cmchar "Tamil 2loop nii";
beginchar(147,2x_height#,body_height#,0);
adjust_fit(0,0);
callchar:="N";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
filldraw stroke z13e---z14e;
filldraw stroke z15e---z16e;
small_iito(16,s,h);
endchar;

cmchar "Tamil 2loop nu";
beginchar(148,2.5x_height#+3u#,x_height#,desc_depth#);
adjust_fit(0,0);
w:=2x_height;
callchar:="N";
tspecs;
h:=oldh; w:=2.5x_height; d:=oldd;
x21=x16+u; y21=y6;
y22=-d; x22=.4[x1,x21];
x23=x1-3u; y23=-.43d; x24=x23+u; y24=-.25d;
pos21(tthick,-1080); pos22(tthin,-1170);
pos23(tthin,-1260); pos24(tthin,-1350);
y25=y24; x25=w-2u; pos25(tthin,-1350);
rt x26r=x25; y26=y25; pos26(tthick,0); x27=x26; y27=h; pos27(tthick,0);
filldraw stroke z13e{right}...{down}z21e...{left}z22e...{up}z23e{up}...{right}z24e--z25e;
filldraw stroke z26e--z27e;
picture V; transform t; t=identity shifted(3u,0);
V=currentpicture transformed t; currentpicture:=V;
endchar;

cmchar "Tamil 2loop nuu";
beginchar(149,2.8x_height#+3u#,x_height#,desc_depth#);
adjust_fit(0,0);
w:=2x_height;
callchar:="N";
tspecs;
h:=oldh; w:=2.5x_height; d:=oldd;
x21=x16+u; y21=y6;
y22=-d; x22=.4[x1,x21];
x23=x1-3u; y23=-.43d; x24=x23+u; y24=-.25d;
pos21(tthick,-1080); pos22(tthin,-1170);
pos23(tthin,-1260); pos24(tthin,-1350);
y25=y24; x25=w-3u; pos25(tthin,-1350);
rt x26r=x25; y26=y25; pos26(tthin,0); x27=x26; y27=h; pos27(tthin,0);
filldraw stroke z13e{right}...{down}z21e...{left}z22e...{up}z23e{up}...{right}z24e--z25e;
filldraw stroke z26e--z27e;
w:=2.8x_height;
x28=lft x27l; top y28r=h; x29=w-u; y29=y28; pos28(tthin,90); pos29(tthin,90);
rt x30r=x29-.7u; y30=h; x31=x30; y31=0; pos30(tthick,0); pos31(tthick,0);
filldraw stroke z28e--z29e; filldraw stroke z30e--z31e;
picture V; transform t; t=identity shifted(3u,0);
V=currentpicture transformed t; currentpicture:=V;
endchar;

cmchar "Tamil 2loop n ai";
beginchar(150,3x_height#,asc_height#,0);
adjust_fit(0,0);
callchar:="N";
dodraw:=false;
tspecs;
dodraw:=true;
h:=oldh; w:=oldw; d:=oldd;
aipoints;
z211=.5[z210,z2]; pos211(tthin,135);
filldraw stroke z201e...{down}z202e...{left}z203e...{up}z204e...z205e...{right}z206e
  ...{down}z207e...z208e...{down}z209e...{right}z210e...z211e
  ...{right}z2e...{down}z3e...{left}z4e..z81e...{right}z5e
  ...{down}z6e...{left}z7e...{up}z8e...{right}z13e---z14e;
filldraw stroke z15e---z16e;
picture V; transform t; t=identity shifted(x_height,0);
V=currentpicture transformed t; currentpicture:=V;
endchar;

cmchar "grantha s dot";
beginchar(176,2.2x_height#,asc_height#,desc_depth#);
adjust_fit(0,0);
callchar:="S";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;

cmchar "grantha s dot i";
beginchar(178,2.5x_height#,body_height#+2u#,desc_depth#);
adjust_fit(0,0);
callchar:="S";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x50=x44; y50=y44; pos50(tthin,90);
x51=x50; top y51l=h; pos51(tthick,-90);
x52=x53=w-u; y52=x_height; y53=y53r=y53l=0; pos52(tthin,-180);
x53r=x53-.6; x53l=x53+.6;
filldraw stroke z50e{left}...{right}z51e...{down}z52e--z53e;
penlabels(50,51,52,53);
endchar;

cmchar "grantha s dot ii";
beginchar(179,2.4x_height#,body_height#+4u#,desc_depth#);
adjust_fit(0,0);
callchar:="S";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x50=x44; y50=y44; pos50(tthin,0);
small_iito(50,s,h);
endchar;

cmchar "grantha s";
beginchar(184,2.4x_height#,x_height#,0);
adjust_fit(0,0);
callchar:="s";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;

cmchar "grantha si";
beginchar(186,2.6x_height#,asc_height#,0);
adjust_fit(0,0);
callchar:="s";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
z82=z16; x83=x16; top y83l=h; pos82(tthin,45); pos83(tthick,-90);
x85=x84=w-u; y84=x_height; y85=y85r=y85l=0; x85r=x85-.6; x85l=x85+.6;
pos84(tthin,-180);
filldraw stroke z82e{left}...{right}z83e...{down}z84e--z85e;
penlabels(83,84,85);
endchar;

cmchar "grantha sii";
beginchar(187,2.4x_height#,body_height#,0);
adjust_fit(0,0);
callchar:="s";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
x100=x16-2.5u; x101=x16+.5u; top y101l=h; pos101(tthick,-90);
y100=.5[y16,y101]; pos100(tthin,0); 
x102=x101+u; y102=y101-.5u; pos102(.7tthick,-135); x103=x102;
y103=y102-2u; pos103(tthin,-270); x104=x104r=x104l=x102;
y104=y101; y104r=y104-.6; y104l=y104+.6;
filldraw stroke z16e{left}...{up}z100e...{right}z101e...z102e...{left}z103e...{right}z104e;
endchar;

cmchar "grantha j";
beginchar(192,1.75x_height#,x_height#,.8x_height#);
adjust_fit(0,0);
callchar:="j";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;


cmchar "grantha ji";
beginchar(194,2.05x_height#,asc_height#,.8x_height#);
adjust_fit(0,0);
callchar:="j";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
z182=z10; x183=x10; top y183l=h; pos182(tthin,90); pos183(tthick,-90);
x185=x184=w-u; y184=x_height; y185=y185r=y185l=0; x185r=x185-.6; x185l=x185+.6;
pos184(tthin,-180);
filldraw stroke z182e{left}...{right}z183e...{down}z184e--z185e;
penlabels(183,184,185);
endchar;

cmchar "grantha jii";
beginchar(195,1.75x_height#,body_height#,.8x_height#);
adjust_fit(0,0);
callchar:="j";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
z82=z10; pos82(tthinner,0); small_iito(82,s,h);
endchar;





cmchar "grantha h";
beginchar(200,3x_height#,x_height#,desc_depth#);
adjust_fit(0,0);
callchar:="h";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
endchar;


cmchar "grantha hi";
beginchar(202,3.2x_height#,asc_height#,desc_depth#);
adjust_fit(0,0);
callchar:="h";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
z180=z106; pos180(tthin,90); x181=x180; top y181l=h; pos181(tthick,-90);
x182=x183=w-u; y182=x_height; y183=y183r=y183l=0; pos182(tthin,-180);
x183r=x183-.6; x183l=x183+.6; 
filldraw stroke z180e{left}...{right}z181e...{down}z182e--z183e;
penlabels(180,181,182,183);
endchar;

cmchar "grantha hii";
beginchar(203,3x_height#,body_height#,desc_depth#);
adjust_fit(0,0);
callchar:="h";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
small_iito(106,s,h);
endchar;

cmchar "grantha ksh";
beginchar(208,3.6x_height#,asc_height#,desc_depth#);
adjust_fit(0,0);
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
y202=y25; x202=x4+2.5u; pos202(tthin,90); x241r=w-u; 
x206=.4[x202,x241]; x207=.3[x202,x241]; x205=.2[x202,x241];
top y205r=x_height; pos205(tthin,-270); y206=.5[y205,y207]; pos206(tthick,-360);
bot y207r=0; pos207(tmed,-450); x240=.8[x207,x241]; bot y240r=0;
pos240(tmed,-450); y241=.5bar_height; pos241(tthick,-360);
x243=.5[x207,x240]; y243=h-y241; z242=.5[z241,z243];
pos242(.5(tthick+tthin),-315); pos243(tthin,-360);
x244=.5[x243,x245]; top y244l=h; pos244(tmed,-450); x245=x243+4u; y245=y243;
pos245(tthin,-540); x246=.5[x207,x243]; bot y246l=-d; pos246(tthinner,-585);
x203=.4[x202,x207]; y203=.5[y202,y204]; pos203(tthick,0); x204=x202;
bot y204r=0; pos204(tthin,-90); x201=2x202-x203; y201=y203; pos201(tthin,-180);
filldraw stroke z25e--z202e{right}...{down}z203e...{left}z204e...z201e...{right}z205e
  ...{down}z206e...{left}z207e;
filldraw stroke z207e--z240e{right}...{up}z241e...{z243-z241}z242e...{up}z243e
  ...{right}z244e...z245e--z246e;
endchar;

cmchar "grantha kshi";
beginchar(210,3.6x_height#+2.6u#,body_height#+2u#,desc_depth#);
adjust_fit(0,0);
callchar:="c";
tspecs;
h:=oldh; w:=3.6x_height; d:=oldd;
y202=y25; x202=x4+2.5u; pos202(tthin,90); x241r=w-u; 
x206=.4[x202,x241]; x207=.3[x202,x241]; x205=.2[x202,x241];
top y205r=x_height; pos205(tthin,-270); y206=.5[y205,y207]; pos206(tthick,-360);
bot y207r=0; pos207(tmed,-450); x240=.8[x207,x241]; bot y240r=0;
pos240(tmed,-450); y241=.5bar_height; pos241(tthick,-360);
x243=.5[x207,x240]; y243=asc_height-y241; z242=.5[z241,z243];
pos242(.5(tthick+tthin),-315); pos243(tthin,-360);
x244=.5[x243,x245]; top y244l=asc_height; pos244(tmed,-450); x245=x243+4u; y245=y243;
pos245(tthin,-540); x246=.5[x207,x243]; bot y246l=-d; pos246(tthinner,-585);
x203=.4[x202,x207]; y203=.5[y202,y204]; pos203(tthick,0); x204=x202;
bot y204r=0; pos204(tthin,-90); x201=2x202-x203; y201=y203; pos201(tthin,-180);
filldraw stroke z25e--z202e{right}...{down}z203e...{left}z204e...z201e...{right}z205e
  ...{down}z206e...{left}z207e;
filldraw stroke z207e--z240e{right}...{up}z241e...{z243-z241}z242e...{up}z243e
  ...{right}z244e...z245e--z246e;
w:=oldw;
x250=x244; top y250l=asc_height; pos250(tthin,-90); x251=x245; top y251=h;
pos251(tthick,90); x252=w-u; y252=x_height; pos252(tthin,0); x253=x252;
y253=y253r=y253l=0; x253l=x253-.6; x253r=x253+.6;
filldraw stroke z250e{left}...{right}z251e...{down}z252e--z253e;
endchar;

cmchar "grantha kshii";
beginchar(211,3.6x_height#,body_height#+4u#,desc_depth#);
adjust_fit(0,0);
callchar:="c";
tspecs;
h:=oldh; w:=oldw; d:=oldd;
y202=y25; x202=x4+2.5u; pos202(tthin,90); x241r=w-u; 
x206=.4[x202,x241]; x207=.3[x202,x241]; x205=.2[x202,x241];
top y205r=x_height; pos205(tthin,-270); y206=.5[y205,y207]; pos206(tthick,-360);
bot y207r=0; pos207(tmed,-450); x240=.8[x207,x241]; bot y240r=0;
pos240(tmed,-450); y241=.5bar_height; pos241(tthick,-360);
x243=.5[x207,x240]; y243=asc_height-y241; z242=.5[z241,z243];
pos242(.5(tthick+tthin),-315); pos243(tthin,-360);
x244=.5[x243,x245]; top y244l=asc_height; pos244(tmed,-450); x245=x243+4u; y245=y243;
pos245(tthin,-540); x246=.5[x207,x243]; bot y246l=-d; pos246(tthinner,-585);
x203=.4[x202,x207]; y203=.5[y202,y204]; pos203(tthick,0); x204=x202;
bot y204r=0; pos204(tthin,-90); x201=2x202-x203; y201=y203; pos201(tthin,-180);
filldraw stroke z25e--z202e{right}...{down}z203e...{left}z204e...z201e...{right}z205e
  ...{down}z206e...{left}z207e;
filldraw stroke z207e--z240e{right}...{up}z241e...{z243-z241}z242e...{up}z243e
  ...{right}z244e...z245e--z246e;
small_iito(244,s,h)
endchar;

cmchar "Tamil ayyatham";
beginchar(240,asc_height#+2u#,asc_height#,0);
adjust_fit(0,0); pickup light_rule.nib; autorounded;
lft x6=hround u; x2=5u-x6; bot y4=0; y2=2u; circle_points; draw_circle;
transform ta; ta=identity shifted(w-5u,0); transform tb; tb=identity shifted(.5w-2.5u,h-6u);
picture VA; VA=currentpicture transformed ta;
picture VB; VB=currentpicture transformed tb;
addto currentpicture also VA; addto currentpicture also VB;
endchar;

% following taken directly from cmr's accent.mf
cmchar "Dot accent";
numeric dot_diam#; dot_diam#=max(dot_size#,cap_curve#);
beginchar(241,5u#,min(asc_height#,10/7x_height#+.5dot_diam#),0);
define_whole_blacker_pixels(dot_diam);
italcorr h#*slant+.5dot_diam#-2u#;
adjust_fit(0,0);
pickup tiny.nib; pos1(dot_diam,0); pos2(dot_diam,90);
x1=x2=.5w; top y2r=h+1;
if bot y2l<x_height+o+slab: y2l:=min(y2r-eps,x_height+o+slab+.5tiny); fi
y1=.5[y2l,y2r]; dot(1,2);  % dot
penlabels(1,2); endchar;

cmchar "grantha u";
beginchar(242,x_height#,asc_height#,0);
adjust_fit(0,0);
x1=u; x2=w-u; top y1r=h; y2=y1; pos1(tthinner,90); pos2(tthinner,90);
rt x3r=x2; x4=x3; y3=h; y4=h-3u; pos3(tthinner,0); pos4(tthinner,0);
x5=x2; bot y5l=y4; x6=x5-3u; y6=y5; pos5(tthinner,90); pos6(tthinner,90);
lft x7l=x6; y7=y4; y8=y3; x8=x7; pos7(tthinner,0); pos8(tthinner,0);
filldraw stroke z1e--z2e;
filldraw stroke z3e--z4e;
filldraw stroke z5e--z6e;
filldraw stroke z7e--z8e;
%picture V; transform t; t=identity shifted(-x_height,0);
%V=currentpicture transformed t; currentpicture:=V;
endchar;

cmchar "grantha uu";
beginchar(243,1.2x_height#,asc_height#+3u#,0);
adjust_fit(0,0);
w:=x_height; h:=asc_height;
x1=u; x2=w-u; top y1r=h; y2=y1; pos1(tthinner,90); pos2(tthinner,90);
rt x3r=x2; x4=x3; y3=h; y4=h-3u; pos3(tthinner,0); pos4(tthinner,0);
x5=x2; bot y5l=y4; x6=x5-3u; y6=y5; pos5(tthinner,90); pos6(tthinner,90);
lft x7l=x6; y7=y4; y8=y3; x8=x7; pos7(tthinner,0); pos8(tthinner,0);
filldraw stroke z1e--z2e;
filldraw stroke z3e--z4e;
filldraw stroke z5e--z6e;
filldraw stroke z7e--z8e;
w:=1.2x_height; h:=asc_height+3u;
x10=w-u; x11=.5[x10,x12]; y11=y4-3u; y10=.5[y9,y11]; top y9l=h; x9=.5[x8,x10];
pos9(tthinner,-90); pos10(tthinner,-180); pos11(tthinner,-270);
y12=y4; x12=x6-2u; pos12(tthinner,-360);
filldraw stroke z8e{up}...{right}z9e...{down}z10e...{left}z11e...{up}z12e;
%picture V; transform t; t=identity shifted(-x_height,0);
%V=currentpicture transformed t; currentpicture:=V;
endchar;


