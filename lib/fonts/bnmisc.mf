%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% bnmisc.mf: METAFONT file that defines some special symbols
% Created by: Abhijit Das (Barda) IISc Bangalore
% Last modified: Jun 20 1997
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar(12,0stwd#,stht#,stdp#); "hasanta";
z1 = (w-0.9pt,-1/4d); z2 = (w+1pt,-5/4d);
pickup pencircle scaled penth#; draw z1..z2;
endchar;

beginchar(13,0.25stwd#,stht#,stdp#); "ja-fala";
pickup pencircle scaled penth#;
z1 = (0,3/4h); z2=(w,3/4h); draw z1..z2;
z3 = (5/6w,3/4h); z4 = (4/9w,7/17h); lft z5 = (0,0);
pickup pencircle xscaled 0.6pt yscaled 0.3pt;
draw z3{dir -180}..tension 1.5 ..{dir -80}z4{dir -80}..{dir -170}z5;
endchar;

beginchar(14,0stwd#,stht#,stdp#); "ref";
z1l + (0.4pt,0) = z1r - (0.4pt,0) = z1 = (-1.4pt,13/16h);
z2l + (0.2pt,0) = z2r - (0.2pt,0) = z2 = (2.5pt,9/8h);
penstroke z1e..z2e;
endchar;

beginchar(15,0stwd#,stht#,stdp#); "ra-fala";
z1 = (w-0.9pt,1/10h); z2 = (w-0.9pt,-d/3); z3 = (w-2.5pt,-1/6d); 
z4 = (w-4.4pt,-1/6d); z5 = (w-5.6pt,1/8h);
pickup pencircle xscaled 0.3pt yscaled 0.6pt;
draw z1--z2{dir 135}..{left}z3..z4{left}..{up}z5;
endchar;

%%% End of bnmisc.mf
